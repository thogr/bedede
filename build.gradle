buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.researchgate:gradle-release:2.2.2'
    }
}

apply from: rootProject.file("release.gradle")

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'checkstyle'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    sourceCompatibility = 1.8
    group = 'com.github.thogr.bedede'

    checkstyle {
        toolVersion = "6.9"
        config = rootProject.checkstyle.config
    }
}

wrapper {
    gradleVersion = '2.7'

    def jvmOpts = "-Xmx1024m"
    inputs.property("jvmOpts", jvmOpts)
    doLast {
        def optsEnvVar = "DEFAULT_JVM_OPTS"
        scriptFile.write scriptFile.text.replace("$optsEnvVar=\"\"", "$optsEnvVar=\"$jvmOpts\"")
        batchScript.write batchScript.text.replace("set $optsEnvVar=", "set $optsEnvVar=$jvmOpts")
    }
}

task printVersion {
    doLast {
        def versionFile = project.file(".release.version")
        versionFile.delete()
        versionFile << project.version
    }
}

ext.libraries = [
    junit: 'junit:junit:4.12',
    mockito : 'org.mockito:mockito-core:1.10.19',
    selenium : 'org.seleniumhq.selenium:selenium-java:2.43.1',
    hamcrest : "org.hamcrest:hamcrest-library:1.3"
]

subprojects {

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        testCompile libraries.junit
        testCompile libraries.mockito

        compile localGroovy()
        testCompile localGroovy()
    }
}


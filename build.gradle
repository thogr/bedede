

allprojects {
    apply plugin: 'eclipse'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    sourceCompatibility = 1.8
    version = '0.2-SNAPSHOT'
    group = 'bedede'
}

wrapper {
    gradleVersion = '2.1'

    def jvmOpts = "-Xmx1024m"
    inputs.property("jvmOpts", jvmOpts)
    doLast {
        def optsEnvVar = "DEFAULT_JVM_OPTS"
        scriptFile.write scriptFile.text.replace("$optsEnvVar=\"\"", "$optsEnvVar=\"$jvmOpts\"")
        batchScript.write batchScript.text.replace("set $optsEnvVar=", "set $optsEnvVar=$jvmOpts")
    }
}

ext.libraries = [
    junit: 'junit:junit:4.11',
    mockito : 'org.mockito:mockito-all:1.10.4',
    selenium : 'org.seleniumhq.selenium:selenium-java:2.43.1'
]

subprojects {

    repositories {
        jcenter()
        mavenCentral()
    }


    sourceSets {
        examples {
            java { srcDir 'src/examples/java' }
            groovy { srcDir 'src/examples/groovy' }
            resources { srcDir 'src/examples/resources' }
        }

        test
        main
    }

    dependencies {
        testCompile libraries.junit
        testCompile libraries.mockito

        examplesCompile sourceSets.main.output
        examplesCompile libraries.junit
        examplesCompile libraries.mockito

        compile localGroovy()
        testCompile localGroovy()
        examplesCompile localGroovy()
    }

    task examples(type: Test) {
        testClassesDir = sourceSets.examples.output.classesDir
        classpath = sourceSets.examples.runtimeClasspath
    }
}

